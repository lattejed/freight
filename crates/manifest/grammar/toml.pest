// Copyright (c) 2023 Matthew Wiriyathananon-Smith
//
// Licensed under the Apache License, Version 2.0
// <LICENSE-APACHE or http://www.apache.org/licenses/LICENSE-2.0> or the MIT
// license <LICENSE-MIT or http://opensource.org/licenses/MIT>, at your
// option. All files in the project carrying such notice may not be copied,
// modified, or distributed except according to those terms.

document = _{ 
                SOI 
                ~ (
                    (
                        NEWLINE*
                        ~ line
                        ~ (NEWLINE* ~ line)*
                        ~ NEWLINE*
                    )
                    |
                    nothing
                  )
                ~ 
                EOI 
            }

line = _{
            (item ~ trailing_comment?)
            |
            line_comment
        }

item = _{ 
            table 
            | 
            key_value 
        }

key_value   =  { identifier ~ "=" ~ value }
value       = _{ array | identifier }

table = { 
            "[" ~ identifier ~ "]" ~ trailing_comment? ~ NEWLINE?
            ~ (
                table_section 
                |
                (key_value ~ trailing_comment? ~ NEWLINE)
                |
                NEWLINE
              )*
        }

table_section = {
                    (table_header ~ NEWLINE)
                    ~ 
                    (key_value ~ trailing_comment? ~ NEWLINE)*
                }

array = {
            "[" 
            ~ (trailing_comment ~ NEWLINE)?
            ~ (
                (
                    value 
                    ~ ("," ~ trailing_comment? ~ value)* 
                    ~ ","? 
                    ~ (trailing_comment ~ NEWLINE)?
                )
                |
                (line_comment ~ NEWLINE)
                |
                NEWLINE
              )*
            ~ 
            "]"
        }

table_header        =  { comment }
trailing_comment    =  { comment }
line_comment        =  { comment }
comment             = _{ "#" ~ (!NEWLINE ~ ANY)* }

identifier  = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }

nothing     = _{ NEWLINE* }
WHITESPACE  = _{ " " | "\t" }
